#!ipxe

# Variables are specified in boot.ipxe.cfg

# Some menu defaults
set menu-timeout 50000
set submenu-timeout ${menu-timeout}

goto start

###################### MAIN MENU ####################################

:start
menu iPXE boot menu
item --gap --             ------------------------- Operating systems ------------------------------
item nixos      Boot NixOS
item win      Boot Windows
item win-install      Boot Windows Installer
item --gap --             ------------------------- Advanced options -------------------------------
item --key c config       Configure settings
item shell                Drop to iPXE shell
item reboot               Reboot computer
item
item --key x exit         Exit iPXE and continue BIOS boot
choose --default exit --timeout ${menu-timeout} target && goto ${target}

:cancel
echo You cancelled the menu, dropping you to a shell

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

:back
set submenu-timeout 0
clear submenu-default
goto start

############ MAIN MENU ITEMS ############

:nixos
echo Booting nixos from iSCSI for ${initiator-iqn}
set root-path ${base-iscsi}:pve
sanboot ${root-path} || goto failed
goto start

:win
echo Booting windows from iSCSI for ${initiator-iqn}
# Force gateway to be the iSCSI target server (kludge for stupid window behavior)
# set netX/gateway ${iscsi-server}
# set net0/gateway 0.0.0.0
set root-path ${base-iscsi}:data
sanboot --drive 0x80 ${root-path} || goto failed
#sanhook --drive 0x81 ${base-iscsi}:wina || goto failed

:win-install
echo Booting windows from iSCSI for ${initiator-iqn}
# Force gateway to be the iSCSI target server (kludge for stupid window behavior)
# set netX/gateway ${iscsi-server}
kernel wimboot
# set net0/gateway 0.0.0.0
#set root-path ${base-iscsi}:win
sanhook --drive 0x80 ${base-iscsi}:data || goto failed
#sanhook --drive 0x81 ${base-iscsi}:wina || goto failed
initrd bootmgr      bootmgr
initrd bootmgr.efi  bootmgr.efi 
initrd bootx64.efi  bootx64.efi
initrd BCD          BCD
initrd boot.sdi     boot.sdi
initrd boot.wim boot.wim
boot

goto start